/*
	Interpretador feito em sala de aula, v. 0
*/
import java.io.*;

COMPILER Calc4

boolean bError;
String errorMsg;

float abs(float v) {
	return v >= 0? v: -v;
}

int fat(int v) {
	return v == 0? 1: v * fat(v-1);
}

void exibir(float v)
{
    if (bError)
        System.out.println("  ERRO: " + errorMsg);
    else if (v % 1 == 0)
        System.out.println("  " + (int)v);
    else
        System.out.println("  " + v);
}

CHARACTERS
	digito = "0123456789".
TOKENS
	numero = digito {digito} [ "." {digito} ].
IGNORE  '\n' + '\r' + '\t'

PRODUCTIONS

Calc4 				(. float v = 0; bError = false; .)
= Exp<out v>		(. exibir(v); .) 
  EOF 
.

Exp<out float v> 	(. float v1, v2, sinal; v1 = v2 = 0; sinal = 1; .)
= ["-"				(. sinal = -1; .)
  ] T<out v1> 		(. v = sinal * v1; .)
  { "+" T<out v2>	(. v += v2; .)
  | "-" T<out v2>	(. v -= v2; .)
  }
.

T<out float v> 		(. float v1, v2; v1 = v2 = 0; .)
= U<out v1> 		(. v = v1; .)
  { "*" U<out v2>	(. v *= v2; .)
  | "/" U<out v2>	(. if (v2 == 0) {
                            errorMsg = "divisao por zero!";
                            bError = true;
                       } else
                            v /= v2; 
  					.)
  }
.

U<out float vu>		(. float v = 0; vu = 0; .) 
= F<out v> 			(. if (v < 0) {
                            errorMsg = "fatorial de negativo!";
                            bError = true;
                        } else
                            vu = v; 
                    .)
    {"!"			(. vu = fat((int)vu); .)
    }
.

F<out float v>		(. float vexp = 0; v = 0; .) 
= numero			(. v = Float.parseFloat(t.val); .)
| "(" Exp<out vexp>	(. v = vexp; .) 
  ")"
| "abs" 	
  "(" Exp<out vexp>	(. v = abs(vexp); .)
  ")"
.

END Calc4.
